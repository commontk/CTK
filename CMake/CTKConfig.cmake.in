
#
# CTKConfig.cmake - CTK CMake configuration file for external projects.
#
# This file is configured by CTK and used by the UseCTK.cmake module
# to load CTK's settings for an external project.


@PACKAGE_INIT@

# CMake extension module directory
set_and_check(CTK_CMAKE_DIR "@PACKAGE_CTK_CMAKE_DIR_CONFIG@")
set_and_check(CTK_CMAKE_UTILITIES_DIR "@PACKAGE_CTK_CMAKE_UTILITIES_DIR_CONFIG@")

# The location of the UseCTK.cmake file.
set_and_check(CTK_USE_FILE "@PACKAGE_CTK_CONFIG_DIR_CONFIG@/UseCTK.cmake")

# Include CTK Plugin specific variables
set_and_check(CTK_PLUGIN_USE_FILE "@PACKAGE_CTK_CONFIG_DIR_CONFIG@/CTKPluginUseFile.cmake")

set_and_check(CTK_TARGETS "@PACKAGE_CTK_CONFIG_DIR_CONFIG@/CTKExports.cmake")

# Directory containing the ctkConfig.h file
set_and_check(CTK_CONFIG_H_INCLUDE_DIR "@PACKAGE_CTK_CONFIG_H_INCLUDE_DIR_CONFIG@")

# Set the header template which defines custom export/import macros
# for shared libraries
set_and_check(CTK_EXPORT_HEADER_TEMPLATE "@PACKAGE_CTK_EXPORT_HEADER_TEMPLATE_DIR_CONFIG@/ctkExport.h.in")

# Set CTK library directory
set_and_check(CTK_LIBRARY_DIR "@PACKAGE_CTK_LIBRARY_DIR_CONFIG@")
set(CTK_LIBRARY_DIRS ${CTK_LIBRARY_DIR})

# CTK specific variables
set(CTK_CMAKE_DEBUG_POSTFIX "@CMAKE_DEBUG_POSTFIX@")

# Import CTK targets
if(NOT TARGET CTKCore)
  include(${CTK_TARGETS})
endif()

@CTK_CONFIG_CODE@

# Include CTK macros
include("${CTK_CMAKE_DIR}/ctkFunctionGetGccVersion.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionCheckCompilerFlags.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionGetCompilerVisibilityFlags.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroParseArguments.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroSetPaths.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroListFilter.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroBuildLibWrapper.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroBuildPlugin.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroBuildQtPlugin.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroTargetLibraries.cmake") # Import multiple macros
include("${CTK_CMAKE_DIR}/ctkFunctionExtractOptionNameAndValue.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroValidateBuildOptions.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionGenerateDGraphInput.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionGeneratePluginManifest.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionGeneratePluginUseFile.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroGeneratePluginResourceFile.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionGetIncludeDirs.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionGetLibraryDirs.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionExtractPluginTargets.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionGetAllPluginTargets.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionGetTargetDependencies.cmake")
include("${CTK_CMAKE_DIR}/ctkFunctionGetPluginDependencies.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroSetupPlugins.cmake")
include("${CTK_CMAKE_DIR}/ctkMacroGenerateMocs.cmake")

# List all libraries
SET(CTK_LIBRARIES @CTK_LIBRARIES@)

# List all CTK libraries wrapped with PythonQt
SET(CTK_WRAPPED_LIBRARIES_PYTHONQT @CTK_WRAPPED_LIBRARIES_PYTHONQT@)

# Include CTK Plugin specific variables
include(${CTK_PLUGIN_USE_FILE})

# CTK external projects variables
@CTK_SUPERBUILD_EP_VARS_CONFIG@

# Qt configuration
set(CTK_QT_VERSION "@CTK_QT_VERSION@")
set(CTK_QT5_COMPONENTS @CTK_QT5_COMPONENTS@)

# Update CMake module path so that calling "find_package(DCMTK)" works as expected 
# after calling "find_package(CTK)"
# Ideally projects like DCMTK or PythonQt should provide both "Config" and "Use" files.
set(CMAKE_MODULE_PATH
  ${CTK_CMAKE_UTILITIES_DIR}
  ${CMAKE_MODULE_PATH}
  )

# Relative install paths
set(CTK_INSTALL_BIN_DIR "@CTK_INSTALL_BIN_DIR@")
set(CTK_INSTALL_INCLUDE_DIR "@CTK_INSTALL_INCLUDE_DIR@")
set(CTK_INSTALL_LIB_DIR "@CTK_INSTALL_LIB_DIR@")
set(CTK_INSTALL_PLUGIN_INCLUDE_DIR "@CTK_INSTALL_PLUGIN_INCLUDE_DIR@")
set(CTK_INSTALL_PLUGIN_DIR "@CTK_INSTALL_PLUGIN_DIR@")
set(CTK_INSTALL_QTPLUGIN_DIR "@CTK_INSTALL_QTPLUGIN_DIR@")

# A CTK install tree always provides one build configuration. A CTK
# build tree may provide either one or multiple build configurations
# depending on the CMake generator used. Since CTK can be used either
# from a build tree or an install tree it is useful for outside
# projects to know the configurations available. If this
# CTKConfig.cmake is in a CTK install tree CTK_CONFIGURATION_TYPES
# will be empty and CTK_BUILD_TYPE will be set to the value of
# CMAKE_BUILD_TYPE used to build CTK. If CTKConfig.cmake is in a CTK
# build tree then CTK_CONFIGURATION_TYPES and CTK_BUILD_TYPE will have
# values matching CMAKE_CONFIGURATION_TYPES and CMAKE_BUILD_TYPE for
# that build tree (only one will ever be set).
SET(CTK_CONFIGURATION_TYPES @CTK_CONFIGURATION_TYPES_CONFIG@)
SET(CTK_BUILD_TYPE @CTK_BUILD_TYPE_CONFIG@)
